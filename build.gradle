// Plugins configuration
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
}

plugins {
	id 'net.researchgate.release' version '2.5.0'
	id 'com.github.johnrengelman.shadow' version '2.0.0'
}

// Projects configuration
allprojects {
	apply plugin: 'java'
}

// Sub-projects configuration
subprojects {
	apply from: "$rootDir/dependencies.gradle"
	apply plugin: 'maven-publish'

	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}

	group = 'com.elo7.nightfall'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	archivesBaseName = 'nightfall'

	publishing {
		publications {
			maven(MavenPublication) {
				artifactId project.projectName
				from components.java
			}
		}
	}

	configurations {
		provided
		compile.extendsFrom provided
	}

	sourceSets {
		main { compileClasspath += configurations.provided }
	}

	// Release specific configurations
	if(project.hasProperty("RELEASE")) {
		apply from: "$rootDir/publish.gradle"
	}
}

// Release definitions
apply plugin: 'net.researchgate.release'

release {
	buildTasks = []
}
// Automatic publication on release
project.tasks.afterReleaseBuild.dependsOn project.getSubprojects().collect({
	it.getTasks().getByName("uploadArchives")
})
